---
title: "ECSã‹ã‚‰S3ã‚’ãƒã‚¦ãƒ³ãƒˆã—ã¦ã¿ãŸ"
emoji: "ğŸª£"
type: "tech" # tech: æŠ€è¡“è¨˜äº‹ / idea: ã‚¢ã‚¤ãƒ‡ã‚¢
topics: ["AWS", "ECS", "S3"]
published: true
---

ã“ã‚“ã«ã¡ã¯ã€‚  
ã”æ©Ÿå«Œã„ã‹ãŒã§ã—ã‚‡ã†ã‹ã€‚  
"No human labor is no human error" ãŒå¤§å¥½ããª[å‰äº• äº®](https://twitter.com/YoshiiRyo1)ã§ã™ã€‚  

Mountpoint for Amazon S3 ãŒç™ºè¡¨ã•ã‚Œã¾ã—ãŸã€‚  

@[card](https://aws.amazon.com/jp/blogs/news/mountpoint-for-amazon-s3-generally-available-and-ready-for-production-workloads/)  

Linux ã‹ã‚‰ç›´æ¥ S3 ãƒã‚±ãƒƒãƒˆã«å¯¾ã—ã¦èª­ã¿æ›¸ãæ©Ÿèƒ½ã‚’æä¾›ã™ã‚‹ã‚½ãƒ•ãƒˆã‚¦ã‚§ã‚¢ã§ã™ã€‚ç´°ã‹ãæ›¸ãã¨ã€èª­ã¿å–ã‚Šã¨æ–°è¦ãƒ•ã‚¡ã‚¤ãƒ«ä½œæˆã‚’è¡Œã†ã‚‚ã®ã§ã™ã€‚  
ãƒ‡ãƒ¼ã‚¿ãƒ¬ã‚¤ã‚¯ã‹ã‚‰ãƒ•ã‚¡ã‚¤ãƒ«ã‚’èª­ã¿å–ã‚Šä½•ã‚‰ã‹ã®å‡¦ç†ã‚’ã—ã¦çµæœãƒ•ã‚¡ã‚¤ãƒ«ã‚’ãƒ‡ãƒ¼ã‚¿ãƒ¬ã‚¤ã‚¯ã¸æˆ»ã™ã€ã¿ãŸã„ãªå‡¦ç†ã«é©åˆ‡ã§ã™ã€‚
å‡¦ç†ã‚’ã™ã‚‹ã‚¢ãƒ—ãƒªã‚±ãƒ¼ã‚·ãƒ§ãƒ³ã«ã‚ˆã£ã¦ã¯ S3 API ã‚’ãƒã‚¤ãƒ†ã‚£ãƒ–ã«ã‚µãƒãƒ¼ãƒˆã—ã¦ã„ãªã„ã“ã¨ã¯çã—ããªã„ã¯ãšã§ã€ãã®éš›ã¯å‰å¾Œã« S3 Get/Put ã™ã‚‹åˆ¥å‡¦ç†ãŒå¿…è¦ã§ã—ãŸãŒã€Mountpoint for Amazon S3 ã«ã‚ˆã£ã¦ãƒ•ã‚¡ã‚¤ãƒ«ã‚·ã‚¹ãƒ†ãƒ ã¨ã—ã¦ãƒã‚¦ãƒ³ãƒˆå¯èƒ½ã«ãªã‚Œã°åˆ¥å‡¦ç†ã¯è¦ã‚‰ãªããªã‚Šã¾ã™ã€‚  

ãŸã ã€S3 ã¯ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆã‚¹ãƒˆãƒ¬ãƒ¼ã‚¸ã§ã‚ã‚‹ã“ã¨ã«ã¯å¤‰ã‚ã‚Šã‚ã‚Šã¾ã›ã‚“ã€‚ãƒ•ã‚¡ã‚¤ãƒ«ã‚µãƒ¼ãƒãƒ¼ã®ã‚ˆã†ãªä½¿ã„æ–¹ã¯ã§ããªã„ã€ã¨ã„ã†ã‹å‘ã‹ãªã„ã®ã§ãƒ•ã‚¡ã‚¤ãƒ«ã‚µãƒ¼ãƒãƒ¼ç”¨é€”ã«ã“ã® Mountpoint for Amazon S3 ã‚’ä½¿ã†ã“ã¨ã¯æ§ãˆãŸã»ã†ãŒã‚ˆã„ã¨æ€ã„ã¾ã™ã€‚  

## ã‚„ã£ã¦ã¿ãŸ

ã‚³ãƒ³ãƒ†ãƒŠã‚¢ãƒ—ãƒªã‚±ãƒ¼ã‚·ãƒ§ãƒ³ã§ã‚‚ S3 ã‹ã‚‰ãƒ‡ãƒ¼ã‚¿ã‚’ Get ã—ã¦å‡¦ç†ã€çµæœã‚’ Put ã¨ã„ã†ãƒ‘ã‚¿ãƒ¼ãƒ³ã¯å¤šãå­˜åœ¨ã™ã‚‹ã¨æƒ³åƒã—ã¦ã„ã¾ã™ã€‚  
ã‚³ãƒ³ãƒ†ãƒŠå†…ã®ä½œæ¥­ãƒ‡ã‚£ãƒ¬ã‚¯ãƒˆãƒªã« Get/Put ã™ã‚‹æ‰‹é–“ã‚’çœãã“ã¨ã«ãªã‚Œã°å¹¸ã›ã¨ã„ã†ã“ã¨ã§ ECS ã‹ã‚‰ S3 ã‚’ãƒã‚¦ãƒ³ãƒˆã—ã¦ã¿ã¾ã™ã€‚  

ä»Šå›ã¯ ECS on EC2 ã§ã™ã€‚  
ECS on Fargate ã§ãƒã‚¦ãƒ³ãƒˆã¯ã¾ã ã§ããªã„ã‚ˆã†ãªã®ã§ã€ECS on Fargate ã§ S3 ãƒã‚±ãƒƒãƒˆä¸Šã®ãƒ•ã‚¡ã‚¤ãƒ«ã‚’èª­ã¿ãŸã„å ´åˆã¯ API ã‚„ AWS CLI ã‚’æ¤œè¨ãã ã•ã„ã€‚  
[Mount failed on ECS Fargate container #450](https://github.com/awslabs/mountpoint-s3/issues/450)  

### Amazon ECS-optimized AMI ã« Mountpoint for Amazon S3 ã‚’ã‚¤ãƒ³ã‚¹ãƒˆãƒ¼ãƒ«

ãƒ‡ãƒ¼ã‚¿ãƒ—ãƒ¬ãƒ¼ãƒ³ã¨ãªã‚‹ EC2 ã¯ Amazon ECS-optimized AMI ã‹ã‚‰èµ·å‹•ã—ã¾ã™ã€‚  
ECS ã‚¯ãƒ©ã‚¹ã‚¿ãƒ¼ã®ã‚­ãƒ£ãƒ‘ã‚·ãƒ†ã‚£ãƒ—ãƒ­ãƒ‘ã‚¤ãƒ€ãƒ¼ã§æŒ‡å®šã—ã¦ã„ã‚‹ã‚ªãƒ¼ãƒˆã‚¹ã‚±ãƒ¼ãƒªãƒ³ã‚°ã‚°ãƒ«ãƒ¼ãƒ—ã®èµ·å‹•ãƒ†ãƒ³ãƒ—ãƒ¬ãƒ¼ãƒˆã«ã¦ãƒ¦ãƒ¼ã‚¶ãƒ¼ãƒ‡ãƒ¼ã‚¿ã‚’è¿½åŠ ã—ã¾ã™ã€‚  

```bash:userdata.sh
## æ—¢å­˜ã®ãƒ¦ãƒ¼ã‚¶ãƒ¼ãƒ‡ãƒ¼ã‚¿ã«ä»¥ä¸‹ã‚’è¿½åŠ 
## mountpoint-3
### Graviton ã‚¤ãƒ³ã‚¹ã‚¿ãƒ³ã‚¹ ã¯ã“ã¡ã‚‰
curl -o /tmp/mount-s3.rpm https://s3.amazonaws.com/mountpoint-s3-release/latest/arm64/mount-s3.rpm
### x86_64 ã¯ã“ã¡ã‚‰
curl -o /tmp/mount-s3.rpm https://s3.amazonaws.com/mountpoint-s3-release/latest/x86_64/mount-s3.rpm
yum install -y /tmp/mount-s3.rpm
mount-s3 your_bucket_name /mnt
rm /tmp/mount-s3.rpm
```

ä¸Šã®ãƒ¦ãƒ¼ã‚¶ãƒ¼ãƒ‡ãƒ¼ã‚¿ãŒæ­£å¸¸ã«å®Ÿè¡Œã•ã‚Œã‚Œã° EC2 ã‚¤ãƒ³ã‚¹ã‚¿ãƒ³ã‚¹ã® /mnt ã« S3 ãƒã‚±ãƒƒãƒˆãŒãƒã‚¦ãƒ³ãƒˆã•ã‚Œã‚‹ã‚ˆã†ã«ãªã‚Šã¾ã™ã€‚ã‚‚ã¡ã‚ã‚“ /mnt ã¯ä»»æ„ã«å¤‰æ›´ã—ã¦å¤§ä¸ˆå¤«ã§ã™ã€‚  


### ã‚¤ãƒ³ã‚¹ã‚¿ãƒ³ã‚¹ãƒ—ãƒ­ãƒ•ã‚¡ã‚¤ãƒ«ã«ãƒãƒªã‚·ãƒ¼ã‚’è¿½åŠ 

ãƒ‡ãƒ¼ã‚¿ãƒ—ãƒ¬ãƒ¼ãƒ³ã¨ãªã‚‹ EC2 ã®ã‚¤ãƒ³ã‚¹ã‚¿ãƒ³ã‚¹ãƒ—ãƒ­ãƒ•ã‚¡ã‚¤ãƒ«ã«ãƒãƒªã‚·ãƒ¼ã‚’è¿½åŠ ã—ã¾ã™ã€‚ãƒã‚¦ãƒ³ãƒˆå…ˆãƒã‚±ãƒƒãƒˆã«å¯¾ã—ã¦5ã¤ã®æ¨©é™ãŒæœ€ä½é™å¿…è¦ãªã‚ˆã†ã§ã™ã€‚  
[Configuring Mountpoint for Amazon S3](https://github.com/awslabs/mountpoint-s3/blob/main/doc/CONFIGURATION.md)  

```json
{
   "Version": "2012-10-17",
   "Statement": [
        {
            "Sid": "MountpointFullBucketAccess",
            "Effect": "Allow",
            "Action": [
                "s3:ListBucket"
            ],
            "Resource": [
                "arn:aws:s3:::your_bucket_name"
            ]
        },
        {
            "Sid": "MountpointFullObjectAccess",
            "Effect": "Allow",
            "Action": [
                "s3:GetObject",
                "s3:PutObject",
                "s3:AbortMultipartUpload",
                "s3:DeleteObject"
            ],
            "Resource": [
                "arn:aws:s3:::your_bucket_name/*"
            ]
        }
   ]
}
```

### ECS ã‚¿ã‚¹ã‚¯å®šç¾©ã§ãƒã‚¤ãƒ³ãƒ‰ãƒã‚¦ãƒ³ãƒˆ

ECS ã‚¿ã‚¹ã‚¯å®šç¾©ã«æ•°è¡Œè¿½åŠ ã—ã¦ãƒã‚¦ãƒ³ãƒˆã—ã¾ã™ã€‚  

**containerPath** ã¯ã‚³ãƒ³ãƒ†ãƒŠå†…ã®ãƒã‚¦ãƒ³ãƒˆãƒã‚¤ãƒ³ãƒˆã§ã™ã€‚  
**sourcePath** ã«ã¯ãƒ¦ãƒ¼ã‚¶ãƒ¼ãƒ‡ãƒ¼ã‚¿ã® `mount-s3 your_bucket_name /mnt` ã®ãƒã‚¦ãƒ³ãƒˆå…ˆã§æŒ‡å®šã—ãŸãƒã‚¦ãƒ³ãƒˆãƒã‚¤ãƒ³ãƒˆã‚’è¨˜è¿°ã—ã¾ã™ã€‚  

```json
    "containerDefinitions": [
      ï¼ˆçœç•¥ï¼‰
        {
            "mountPoints": [
                {
                    "sourceVolume": "s3root",
                    "containerPath": "/mnt"
                }
        }
      ï¼ˆçœç•¥ï¼‰
    ],
    ï¼ˆçœç•¥ï¼‰
    "volumes": [
        {
            "name": "s3root",
            "host": {
                "sourcePath": "/mnt"
            }
        }
    ],
ï¼ˆçœç•¥ï¼‰
```

### ã‚¿ã‚¹ã‚¯å®Ÿè¡Œ

`ls /mnt` ã‚’æ‰“ã¤ã ã‘ã®ã‚¿ã‚¹ã‚¯ã‚’ä½œã£ã¦å‹•ä½œç¢ºèªã—ã¾ã—ãŸã€‚  

:::details ã‚¯ãƒªãƒƒã‚¯ã—ã¦å±•é–‹
```json
{
    "containerDefinitions": [
        {
            "name": "ryoyoshii_mount_s3",
            "image": "public.ecr.aws/amazonlinux/amazonlinux:2023",
            "cpu": 0,
            "portMappings": [],
            "essential": true,
            "command": [
                "ls",
                "/mnt"
            ],
            "environment": [],
            "mountPoints": [
                {
                    "sourceVolume": "s3root",
                    "containerPath": "/mnt"
                }
            ],
            "volumesFrom": [],
            "logConfiguration": {
                "logDriver": "awslogs",
                "options": {
                    "awslogs-group": "/ecs/cluster_name",
                    "awslogs-region": "us-west-2",
                    "awslogs-stream-prefix": "mount_s3"
                }
            }
        }
    ],
    "family": "ryoyoshii_mount_s3",
    "taskRoleArn": "your_Task_RoleArn",
    "executionRoleArn": "your_Task_Execution_RoleArn",
    "networkMode": "awsvpc",
    "volumes": [
        {
            "name": "s3root",
            "host": {
                "sourcePath": "/mnt"
            }
        }
    ],
    "requiresCompatibilities": [
        "EC2"
    ],
    "cpu": "512",
    "memory": "1024",
}
```
:::

### terraform

ä¸€é€£ã®å‹•ä½œç¢ºèªãŒã§ãã‚‹ tf ãƒ•ã‚¡ã‚¤ãƒ«ã‚’è¨˜è¿°ã—ã¦ãŠãã¾ã™ã€‚ã‚ˆã—ãªã«ç·¨é›†ã—ã¦ãŠä½¿ã„ãã ã•ã„ã€‚  

:::details ã‚¯ãƒªãƒƒã‚¯ã—ã¦å±•é–‹
```HCL
locals {
  // General
  sysname = "ryoyoshii_test"
  region  = "us-west-2"

  // Network
  vpcid = "vpc-"
  subnet = {
    public_a = "subnet-"
    public_b = "subnet-"
    public_c = "subnet-"
  }

  work_bucket_name = "ryoyoshii-tfstate"
}

// ECS Cluster
resource "aws_ecs_cluster" "this" {
  name = local.sysname

  setting {
    name  = "containerInsights"
    value = "enabled"
  }

  tags = {
    Name = "${local.sysname}"
  }
}
// End of ECS Cluster

// Task definition
//// mount-s3
resource "aws_ecs_task_definition" "mount_s3" {
  family = "${local.sysname}_mount_s3"
  container_definitions = jsonencode([
    {
      "logConfiguration" : {
        "logDriver" : "awslogs",
        "options" : {
          "awslogs-group" : aws_cloudwatch_log_group.ecs.name,
          "awslogs-region" : "${local.region}",
          "awslogs-stream-prefix" : "mount_s3"
        }
      },
      "command" : [
        "ls",
        "/mnt"
      ],
      "essential" : true,
      "volumesFrom" : [],
      "mountPoints" : [
        {
          "sourceVolume" : "s3root",
          "containerPath" : "/mnt"
        }
      ],
      "image" : "public.ecr.aws/amazonlinux/amazonlinux:2023",
      "name" : "${local.sysname}_mount_s3"
    }
  ])

  volume {
    name      = "s3root"
    host_path = "/mnt"
  }

  task_role_arn            = aws_iam_role.ecs_Task.arn
  execution_role_arn       = aws_iam_role.ecs_TaskExecution.arn
  requires_compatibilities = ["EC2"]
  cpu                      = 512
  memory                   = 1024
  network_mode             = "awsvpc"
}
// End of Task definition

// Security Group ECS
resource "aws_security_group" "ecs" {
  name        = "${local.sysname}_ecs_sg"
  description = "ECS ${local.sysname}"
  vpc_id      = local.vpcid

  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }

  tags = {
    Name = "${local.sysname}_ecs_sg"
  }
}
// End Of Security Group ECS

// IAM Role TaskExecution
resource "aws_iam_role" "ecs_TaskExecution" {
  name = "CustomRoleTaskExecution_${local.sysname}"

  assume_role_policy = jsonencode({
    "Version" : "2012-10-17",
    "Statement" : [
      {
        "Effect" : "Allow",
        "Principal" : {
          "Service" : "ecs-tasks.amazonaws.com"
        },
        "Action" : "sts:AssumeRole"
      }
    ]
  })
}

resource "aws_iam_role_policy_attachment" "ecs_TaskExecution" {
  role       = aws_iam_role.ecs_TaskExecution.name
  policy_arn = "arn:aws:iam::aws:policy/service-role/AmazonECSTaskExecutionRolePolicy"
}
// End Of IAM Role TaskExecution

// IAM Role ECS Task
resource "aws_iam_role" "ecs_Task" {
  name = "CustomRoleTask_${local.sysname}"

  assume_role_policy = jsonencode({
    "Version" : "2012-10-17",
    "Statement" : [
      {
        "Effect" : "Allow",
        "Principal" : {
          "Service" : "ecs-tasks.amazonaws.com"
        },
        "Action" : "sts:AssumeRole"
      }
    ]
  })


  inline_policy {
    name = "CustomRoleTask_${local.sysname}"

    policy = jsonencode({
      Version = "2012-10-17"
      Statement = [
        {
          "Effect" : "Allow",
          "Action" : "s3:ListAllMyBuckets",
          "Resource" : "arn:aws:s3:::*"
        },
        {
          "Effect" : "Allow",
          "Action" : [
            "s3:ListBucket",
            "s3:ListBucketVersions"
          ]
          "Resource" : "arn:aws:s3:::${local.work_bucket_name}"
        },
        {
          "Effect" : "Allow",
          "Action" : [
            "s3:GetObject",
            "s3:PutObject"
          ],
          "Resource" : "arn:aws:s3:::${local.work_bucket_name}/*"
        }
      ]
    })
  }
}

resource "aws_iam_role_policy_attachment" "ecs_Task" {
  role       = aws_iam_role.ecs_Task.name
  policy_arn = "arn:aws:iam::aws:policy/service-role/AmazonECSTaskExecutionRolePolicy"
}
// End Of IAM Role ECS Task

// CloudWatch Logs Group
resource "aws_cloudwatch_log_group" "ecs" {
  name              = "/ecs/${local.sysname}"
  retention_in_days = 7
}
// End Of CloudWatch Logs Group

// ECS on EC2 s3 mounted
data "aws_ssm_parameter" "ecs_ami_id" {
  name = "/aws/service/ecs/optimized-ami/amazon-linux-2023/arm64/recommended/image_id"
}

// ecs capacity provider
resource "aws_ecs_cluster_capacity_providers" "this" {
  cluster_name = aws_ecs_cluster.this.name

  capacity_providers = [
    aws_ecs_capacity_provider.this.name,
  ]

  default_capacity_provider_strategy {
    base              = 0
    weight            = 1
    capacity_provider = aws_ecs_capacity_provider.this.name
  }
}

resource "aws_ecs_capacity_provider" "this" {
  name = local.sysname

  auto_scaling_group_provider {
    auto_scaling_group_arn = aws_autoscaling_group.this.arn
    managed_scaling {
      maximum_scaling_step_size = 10000
      minimum_scaling_step_size = 1
      status                    = "ENABLED"
      target_capacity           = 100
    }
  }
}

resource "aws_autoscaling_group" "this" {
  name                      = local.sysname
  max_size                  = 1
  min_size                  = 0
  health_check_grace_period = 0
  health_check_type         = "EC2"
  desired_capacity          = 0
  vpc_zone_identifier = [
    local.subnet.public_a,
    local.subnet.public_b,
    local.subnet.public_c
  ]

  mixed_instances_policy {
    instances_distribution {
      on_demand_base_capacity                  = 0
      on_demand_percentage_above_base_capacity = 0
      spot_allocation_strategy                 = "capacity-optimized"
    }

    launch_template {
      launch_template_specification {
        launch_template_id = aws_launch_template.this.id
      }
    }
  }

  tag {
    key                 = "AmazonECSManaged"
    value               = ""
    propagate_at_launch = true
  }
}

resource "aws_launch_template" "this" {
  name     = local.sysname
  image_id = data.aws_ssm_parameter.ecs_ami_id.value
  iam_instance_profile {
    name = aws_iam_instance_profile.this.name
  }
  user_data              = filebase64("userdata.sh")
  update_default_version = true
  vpc_security_group_ids = [aws_security_group.this.id]

  instance_requirements {
    vcpu_count {
      min = 1
      max = 4
    }
    memory_mib {
      min = 1024
      max = 8096
    }
    instance_generations = ["current"]
  }

  block_device_mappings {
    device_name = "/dev/xvda"

    ebs {
      delete_on_termination = "true"
      encrypted             = "true"
      volume_size           = 30
      volume_type           = "gp3"
    }
  }

  tag_specifications {
    resource_type = "instance"

    tags = {
      Name = local.sysname
    }
  }
}
// End of ecs capacity provider


// Security Group EC2
resource "aws_security_group" "this" {
  name        = "${local.sysname}-ec2-sg"
  description = "ECS on EC2"
  vpc_id      = local.vpcid

  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }

  tags = {
    Name = "${local.sysname}-ec2-sg"
  }
}
// End of Security Group EC2


// Instance Profile
data "aws_iam_policy_document" "this" {
  statement {
    actions = ["sts:AssumeRole"]

    principals {
      type = "Service"
      identifiers = [
        "ec2.amazonaws.com"
      ]
    }
  }
}

data "aws_iam_policy_document" "inline_policy" {
  statement {
    actions   = ["s3:ListBucket"]
    effect    = "Allow"
    resources = ["arn:aws:s3:::${local.work_bucket_name}"]
  }

  statement {
    actions = [
      "s3:GetObject",
      "s3:PutObject",
      "s3:AbortMultipartUpload",
      "s3:DeleteObject"
    ]
    effect    = "Allow"
    resources = ["arn:aws:s3:::${local.work_bucket_name}/*"]
  }
}

resource "aws_iam_role" "this" {
  name               = "CustomRole-${local.sysname}-ECSonEC2"
  assume_role_policy = data.aws_iam_policy_document.this.json

  inline_policy {
    name   = "my_inline_policy"
    policy = data.aws_iam_policy_document.inline_policy.json
  }
}

resource "aws_iam_instance_profile" "this" {
  name = "CustomRole-${local.sysname}-ECSonEC2"
  role = aws_iam_role.this.name
}

resource "aws_iam_role_policy_attachment" "this" {
  role       = aws_iam_role.this.name
  policy_arn = "arn:aws:iam::aws:policy/AmazonSSMManagedInstanceCore"
}

resource "aws_iam_role_policy_attachment" "ecs_ec2_role" {
  role       = aws_iam_role.this.name
  policy_arn = "arn:aws:iam::aws:policy/service-role/AmazonEC2ContainerServiceforEC2Role"
}
// End Of Instance Profile
```
:::


## Dockerfileã«æ›¸ããƒ‘ã‚¿ãƒ¼ãƒ³

ãƒ›ã‚¹ãƒˆã§ãƒã‚¦ãƒ³ãƒˆã›ãšã¨ã‚‚ Dockerfile ã« Mountpoint for Amazon S3 ã‚’ã‚¤ãƒ³ã‚¹ãƒˆãƒ¼ãƒ«ã™ã‚‹ãƒ‘ã‚¿ãƒ¼ãƒ³ã‚‚å¯èƒ½ã§ã™ã€‚å˜ç´”ãªå„ªåŠ£ã¯ãªã„ã¨æ€ã†ã®ã§è¦ä»¶ã«åˆã‚ã›ã¦ä½¿ã„åˆ†ã‘ã‚‹ã¨ä¾¿åˆ©ã ã¨æ€ã„ã¾ã™ã€‚    
[Running Mountpoint for Amazon S3 in a Docker container](https://github.com/awslabs/mountpoint-s3/blob/main/docker/README.md)  

## å‚è€ƒ

ECS ã§ã¯ãªã„æ™®é€šã® EC2 ã‚¤ãƒ³ã‚¹ã‚¿ãƒ³ã‚¹ã§ãƒã‚¦ãƒ³ãƒˆã—ãŸã„å ´åˆã¯ [AWSè£½ã®S3ãƒã‚¦ãƒ³ãƒˆãƒ„ãƒ¼ãƒ«ã€ŒMountpoint for Amazon S3ã€ã‚’è©¦ã—ã¦ã¿ãŸ](https://dev.classmethod.jp/articles/mountpoint-for-amazon-s3/) ã‚’å‚ç…§ãã ã•ã„ã€‚  

